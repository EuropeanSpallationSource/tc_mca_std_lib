<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="FB_Axis" Id="{45901cd0-c6d2-4114-b7cf-de832171219f}" SpecialFunc="None">
    <Declaration><![CDATA[///#########################################################
///Function block to run a virtual drive with Nc
/// Library:
/// Tc2_MC2.lib
///
/// Global Variables:
///
/// Data types:
///
/// External functions:
///
///###########################################################
FUNCTION_BLOCK FB_Axis
VAR
    sVersion: STRING:='1.0.4';
    masterAxis          :   ST_GearAxis :=(index:=1, ratio:=0) ;

    i: UINT;
END_VAR
VAR_INPUT

END_VAR
VAR_OUTPUT

END_VAR

VAR_IN_OUT
    stAxisStruct: ST_AxisStruct;
END_VAR
VAR
    fbReset: MC_Reset;
    fbPower: MC_Power;
    fbStop: MC_Stop;
    fbHalt: MC_Halt;
    fbJog: MC_Jog;
    fbMoveAbsolute: MC_MoveAbsolute;
    fbMoveVelocity: MC_MoveVelocity;
    fbMoveRelative: MC_MoveRelative;
    fbMoveModulo: MC_MoveModulo;
    fbGearIn: MC_GearIn;
    fbGearOut: MC_GearOut;
    fbHome: FB_Homing;
    fbExecuteRiseEdge: R_TRIG;
    fbStopRiseEdge: R_TRIG;
    fbResetRiseEdge: R_TRIG;

    fbTonStartupDelay : TON:=(IN:=TRUE,PT:=T#5S);
    fbTrigStartup     : R_TRIG;

    fbWriteSoftLimMax : FB_WriteFloatParameter;
    fbWriteSoftLimMin : FB_WriteFloatParameter;
    fbWriteSoftLimMaxEnable : FB_WriteParameterInNc_v1_00;
    fbWriteSoftLimMinEnable : FB_WriteParameterInNc_v1_00;
    fbWriteHomingVelocityToCam : FB_WriteFloatParameter;
    fbWriteHomingVelocityFromCam : FB_WriteFloatParameter;
    fbWriteTargetWindowPosition : FB_WriteFloatParameter;
    fbWriteLagError : FB_WriteFloatParameter;
    fbWriteMaxVelocity : FB_WriteFloatParameter;
    fbWriteMaxAcceleration : FB_WriteFloatParameter;
    fbWriteMaxDeceleration : FB_WriteFloatParameter;
    fbWriteDefJerk : FB_WriteFloatParameter;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[stAxisStruct.Axis.ReadStatus();
A_SetParameters();
////Call of all MC Instances//////

fbPower(Axis:=stAxisStruct.Axis);
fbPower.Enable:= FALSE;
fbReset(Axis:=stAxisStruct.Axis);
fbReset.Execute:= FALSE;
fbStop(Axis:=stAxisStruct.Axis);
fbStop.Execute:=FALSE;
fbHalt(Axis:=stAxisStruct.Axis);
fbHalt.Execute:=FALSE;
fbJog(Axis:=stAxisStruct.Axis);
fbHome(Axis:=stAxisStruct.Axis);
fbHome.bExecute:=FALSE;
fbMoveAbsolute(Axis:=stAxisStruct.Axis);
fbMoveAbsolute.Execute:=FALSE;
fbMoveRelative(Axis:=stAxisStruct.Axis);
fbMoveRelative.Execute:=FALSE;
fbMoveVelocity(Axis:=stAxisStruct.Axis);
fbMoveVelocity.Execute:=FALSE;
fbMoveModulo(Axis:=stAxisStruct.Axis);
fbMoveModulo.Execute:=FALSE;
fbGearin(Slave:= stAxisStruct.Axis, Master:= GVL.axes[masterAxis.index].Axis);
fbGearIn.Execute:=FALSE;
fbGearOut(Slave:=stAxisStruct.Axis);
fbGearOut.Execute:=FALSE;

stAxisStruct.status.bFwEnabled:= stAxisStruct.inputs.bLimitFwd;
stAxisStruct.status.bBwEnabled:= stAxisStruct.inputs.bLimitBwd;

FOR i:= 1 TO GVL_APP.axisNum DO
    IF (stAxisStruct.slaveAxisRatio[i]<>0) THEN
        stAxisStruct.status.bFwEnabled:=stAxisStruct.status.bFwEnabled AND SEL(stAxisStruct.slaveAxisRatio[i]>0, GVL.axes[i].status.bBwEnabled, GVL.axes[i].status.bFwEnabled);
        stAxisStruct.status.bBwEnabled:=stAxisStruct.status.bBwEnabled AND SEL(stAxisStruct.slaveAxisRatio[i]>0, GVL.axes[i].status.bFwEnabled, GVL.axes[i].status.bBwEnabled);
    END_IF
END_FOR

(*Power*)
    fbPower.Enable:=stAxisStruct.control.bEnable;
    fbPower.Enable_Positive:= stAxisStruct.status.bFwEnabled;
    fbPower.Enable_Negative:= stAxisStruct.status.bBwEnabled;
    fbPower.Override:= stAxisStruct.config.fOverride;
    stAxisStruct.status.bEnabled:=fbPower.Status;

(*Stop*)
    fbStop.Execute:= stAxisStruct.control.bStop;
    fbStop.Deceleration:= stAxisStruct.config.stDeceleration.Value;

IF stAxisStruct.control.bStop AND stAxisStruct.Axis.Status.Stopping THEN
    stAxisStruct.control.bStop:=FALSE;
END_IF

(*Reset*)
    fbReset.Execute:=stAxisStruct.control.bReset AND stAxisStruct.status.bError;

IF stAxisStruct.control.bReset AND NOT (stAxisStruct.status.bError OR stAxisStruct.status.bWarning) THEN
    stAxisStruct.control.bReset:=FALSE;
END_IF

CASE stAxisStruct.control.eCommand OF
    MotionFunctions.MoveAbsolute:
        fbMoveAbsolute.Execute:=stAxisStruct.control.bExecute;
        fbMoveAbsolute.Position:=stAxisStruct.config.stPosition.Value;
        fbMoveAbsolute.Velocity:=ABS(stAxisStruct.config.stVelocity.Value);
        fbMoveAbsolute.Acceleration:=stAxisStruct.config.stAcceleration.Value;
        fbMoveAbsolute.Deceleration:=stAxisStruct.config.stDeceleration.Value;

    MotionFunctions.MoveRelative:
        fbMoveRelative.Execute:=stAxisStruct.control.bExecute;
        fbMoveRelative.Distance:=stAxisStruct.config.stPosition.Value;
        fbMoveRelative.Velocity:=ABS(stAxisStruct.config.stVelocity.Value);
        fbMoveRelative.Acceleration:=stAxisStruct.config.stAcceleration.Value;
        fbMoveRelative.Deceleration:=stAxisStruct.config.stDeceleration.Value;

    MotionFunctions.Jog:
        fbJog.JogForward:=stAxisStruct.control.bJogFwd AND NOT (stAxisStruct.control.bExecute OR stAxisStruct.status.bError);
        fbJog.JogBackwards:=stAxisStruct.control.bJogBwd AND NOT (stAxisStruct.control.bExecute OR stAxisStruct.status.bError);
        fbJog.Mode:=0;
        fbJog.Velocity:=stAxisStruct.config.stVelocity.Value;
        fbJog.Acceleration:=stAxisStruct.config.stAcceleration.Value;
        fbJog.Deceleration:=stAxisStruct.config.stDeceleration.Value;

    MotionFunctions.MoveVelocity:
        fbMoveVelocity.Execute:=stAxisStruct.control.bExecute;
        fbMoveVelocity.Velocity:=ABS(stAxisStruct.config.stVelocity.Value);
        fbMoveVelocity.Acceleration:=stAxisStruct.config.stAcceleration.Value;
        fbMoveVelocity.Deceleration:=stAxisStruct.config.stDeceleration.Value;
        fbMoveVelocity.Direction:=SEL(stAxisStruct.config.stVelocity.Value<0, MC_Positive_Direction, MC_Negative_Direction);

    MotionFunctions.Movemodulo:
        fbMoveModulo.Execute:=stAxisStruct.control.bExecute;
        fbMoveModulo.Position:=stAxisStruct.config.stPosition.Value;
        fbMoveModulo.Velocity:=ABS(stAxisStruct.config.stVelocity.Value);
        fbMoveModulo.Acceleration:=stAxisStruct.config.stAcceleration.Value;
        fbMoveModulo.Deceleration:=stAxisStruct.config.stDeceleration.Value;


    MotionFunctions.GearIn:
        fbGearIn.Execute:=          stAxisStruct.control.bExecute;
        fbGearIn.RatioNumerator:=   masterAxis.Ratio;
        fbGearIn.RatioDenominator:=1;       //kept static as numerator is LREAL and consistent with multigearing

        IF NOT fbGearIn.InGear THEN
            masterAxis.Index:=  stAxisStruct.masterAxis.index;
            masterAxis.Ratio:= stAxisStruct.masterAxis.ratio;
        END_IF

        IF fbGearIn.InGear AND (GVL.axes[masterAxis.Index].slaveAxisRatio[GVL.iAxis]=0) THEN    //only update index and ratios on new gearIns, change of ratio requires gearOut
            GVL.axes[masterAxis.index].slaveAxisRatio[GVL.iAxis]:=masterAxis.ratio;
        END_IF

    MotionFunctions.GearOut:
        fbGearOut.Execute:=         stAxisStruct.control.bExecute;

        IF fbGearOut.Done THEN
            GVL.axes[masterAxis.index].slaveAxisRatio[GVL.iAxis]:=0; //Clear slave ratio from master axis slave array
        END_IF

    MotionFunctions.MultiGearIn:

    MotionFunctions.Home:
        fbHome.bExecute:= stAxisStruct.control.bExecute;
        fbHome.bReset:= stAxisStruct.control.bReset;
        fbHome.bStop:= stAxisStruct.control.bStop;
        fbHome.nHomeProc:= stAxisStruct.config.nHomeSeq;
        fbHome.fHomePosition:= stAxisStruct.config.stHomePosition.Value;
        fbHome.bLimitBwd:= stAxisStruct.inputs.bLimitBwd;
        fbHome.bLimitFwd:= stAxisStruct.inputs.bLimitFwd;
        fbHome.bEncLatch:= stAxisStruct.inputs.bEncLatch;
        fbHome.bHomeSensor:= stAxisStruct.inputs.bHomeSensor;
        fbHome.fHomePosition:= stAxisStruct.config.stHomePosition.Value;
        stAxisStruct.status.bHomed:=fbHome.bHomed;

END_CASE

(*Busy*)
stAxisStruct.status.bBusy:=stAxisStruct.Axis.Status.HasJob;

(*Done*)
IF fbMoveAbsolute.Done OR fbMoveModulo.Done OR fbMoveRelative.Done OR fbHome.fbFinishHoming.Done THEN
        stAxisStruct.status.bDone:=TRUE;
ELSIF stAxisStruct.Axis.Status.HasJob THEN
        stAxisStruct.status.bDone:=FALSE;
END_IF

(*Error from functions and Nc*)
IF stAxisStruct.status.bError and not stAxisStruct.control.bReset THEN
    stAxisStruct.status.bError:=stAxisStruct.status.bError;
    stAxisStruct.status.nErrorID:=stAxisStruct.status.nErrorID;
ELSIF fbPower.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbPower.ErrorID;
ELSIF fbHalt.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbHalt.ErrorID;
ELSIF fbJog.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbJog.ErrorID;
ELSIF fbMoveVelocity.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbMoveVelocity.ErrorID;
ELSIF fbMoveRelative.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbMoveRelative.ErrorID;
ELSIF fbMoveAbsolute.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbMoveAbsolute.ErrorID;
ELSIF fbMoveModulo.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbMoveModulo.ErrorID;
ELSIF fbHome.bError THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbHome.nErrorID;
ELSIF fbGearIn.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbGearIn.ErrorID;
ELSIF fbGearIn.Error THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbGearIn.ErrorID;
ELSIF fbGearOut.Error and stAxisStruct.Axis.Status.Coupled THEN
    stAxisStruct.status.bError:=TRUE;
    stAxisStruct.status.nErrorId:=fbGearOut.ErrorID;
ELSIF stAxisStruct.Axis.Status.Error  THEN
    stAxisStruct.status.bError:=stAxisStruct.Axis.Status.Error;
    stAxisStruct.status.nErrorId:=stAxisStruct.Axis.Status.ErrorID;
ELSE
    stAxisStruct.status.bError:=FALSE;
    stAxisStruct.status.nErrorId:=0;
END_IF;

(*Warnings from functions and Nc*)
IF stAxisStruct.status.bWarning AND NOT stAxisStruct.control.bReset THEN
    stAxisStruct.status.bWarning:=stAxisStruct.status.bWarning;
    //stAxisStruct.status.nWarningID:=stAxisStruct.status.nWarningID;
ELSIF fbHome.bWarning THEN
    stAxisStruct.status.bWarning:=stAxisStruct.status.bError;
    //stAxisStruct.status.nWarningID:=stAxisStruct.status.nWarningID;
ELSE
    stAxisStruct.status.bWarning:=FALSE;
    //stAxisStruct.status.nWarningId:=0;
END_IF;

stAxisStruct.status.bGeared:= stAxisStruct.Axis.Status.Coupled;

(*Actual Velocity*)
stAxisStruct.status.fActVelocity:=stAxisStruct.Axis.NcToPlc.ActVelo;

(*Actual Position*)
IF stAxisStruct.Axis.Status.OpMode.Modulo THEN
    stAxisStruct.status.fActPosition:=stAxisStruct.Axis.NcToPlc.ModuloActPos;
ELSE
    stAxisStruct.status.fActPosition:=stAxisStruct.Axis.NcToPlc.ActPos;
END_IF

(*Reset bExecute*)
stAxisStruct.control.bExecute:=FALSE;]]></ST>
    </Implementation>
    <Action Name="A_SetParameters" Id="{9b021678-0de9-402c-b544-5e0ea3d91be0}">
      <Implementation>
        <ST><![CDATA[fbTonStartupDelay();
fbTrigStartup(CLK:=fbTonStartupDelay.q , Q=> );

fbWriteSoftLimMin(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stUserMin.bWrite,
    nDeviceGroup:= 16#5000,
    nIndexOffset:= 16#D,
    nData:= stAxisStruct.config.stUserMin.Value,
    bBusy=> stAxisStruct.config.stUserMin.bBusy,
    bDone=> stAxisStruct.config.stUserMin.bDone,
    bError=> stAxisStruct.config.stUserMin.bError,
    Axis:= stAxisStruct.Axis );

fbWriteSoftLimMinEnable(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stSoftLimMinEnable.bWrite,
    nDeviceGroup:= 16#5000,
    nIndexOffset:= 16#B,
    nData:= BOOL_TO_DWORD(stAxisStruct.config.stSoftLimMinEnable.Value),
    bBusy=> stAxisStruct.config.stSoftLimMinEnable.bBusy,
    bDone=> stAxisStruct.config.stSoftLimMinEnable.bDone,
    bError=> stAxisStruct.config.stSoftLimMinEnable.bError,
    Axis:= stAxisStruct.Axis );

fbWriteSoftLimMax(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stUserMax.bWrite,
    nDeviceGroup:= 16#5000,
    nIndexOffset:= 16#E,
    nData:= stAxisStruct.config.stUserMax.Value,
    bBusy=> stAxisStruct.config.stUserMax.bBusy,
    bDone=> stAxisStruct.config.stUserMax.bDone,
    bError=> stAxisStruct.config.stUserMax.bError,
    Axis:= stAxisStruct.Axis );

fbWriteSoftLimMaxEnable(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stSoftLimMaxEnable.bWrite,
    nDeviceGroup:= 16#5000,
    nIndexOffset:= 16#C,
    nData:= BOOL_TO_DWORD(stAxisStruct.config.stSoftLimMaxEnable.Value),
    bBusy=> stAxisStruct.config.stSoftLimMaxEnable.bBusy,
    bDone=> stAxisStruct.config.stSoftLimMaxEnable.bDone,
    bError=> stAxisStruct.config.stSoftLimMaxEnable.bError,
    Axis:= stAxisStruct.Axis );

fbWriteHomingVelocityToCam(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stHomingVelocityToCam.bWrite,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#6,
    nData:= stAxisStruct.config.stHomingVelocityToCam.Value,
    bBusy=> stAxisStruct.config.stHomingVelocityToCam.bBusy,
    bDone=> stAxisStruct.config.stHomingVelocityToCam.bDone,
    bError=> stAxisStruct.config.stHomingVelocityToCam.bError,
    Axis:= stAxisStruct.Axis );

fbWriteHomingVelocityFromCam(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stHomingVelocityFromCam.bWrite,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#7,
    nData:= stAxisStruct.config.stHomingVelocityFromCam.Value,
    bBusy=> stAxisStruct.config.stHomingVelocityFromCam.bBusy,
    bDone=> stAxisStruct.config.stHomingVelocityFromCam.bDone,
    bError=> stAxisStruct.config.stHomingVelocityFromCam.bError,
    Axis:= stAxisStruct.Axis );

fbWriteTargetWindowPosition(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stTargetPositionWindow.bWrite,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#16,
    nData:= stAxisStruct.config.stTargetPositionWindow.Value,
    bBusy=> stAxisStruct.config.stTargetPositionWindow.bBusy,
    bDone=> stAxisStruct.config.stTargetPositionWindow.bDone,
    bError=> stAxisStruct.config.stTargetPositionWindow.bError,
    Axis:= stAxisStruct.Axis );

fbWriteLagError(
    bExecute:= fbTrigStartup.Q OR stAxisStruct.config.stLagError.bWrite,
    nDeviceGroup:= 16#6000,
    nIndexOffset:= 16#12,
    nData:= stAxisStruct.config.stLagError.Value,
    bBusy=> stAxisStruct.config.stLagError.bBusy,
    bDone=> stAxisStruct.config.stLagError.bDone,
    bError=> stAxisStruct.config.stLagError.bError,
    Axis:= stAxisStruct.Axis );

fbWriteMaxVelocity(
    bExecute:= fbTrigStartup.Q,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#27,
    nData:= stAxisStruct.config.fMaximumVelocity,
    bBusy=>,
    bDone=>,
    bError=>,
    Axis:= stAxisStruct.Axis );

fbWriteMaxAcceleration(
    bExecute:= fbTrigStartup.Q,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#F1,
    nData:= stAxisStruct.config.fMaximumAcceleration,
    bBusy=>,
    bDone=>,
    bError=>,
    Axis:= stAxisStruct.Axis );

fbWriteMaxDeceleration(
    bExecute:= fbTrigStartup.Q,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#F2,
    nData:= stAxisStruct.config.fMaximumDeceleration,
    bBusy=>,
    bDone=>,
    bError=>,
    Axis:= stAxisStruct.Axis );

fbWriteDefJerk(
    bExecute:= fbTrigStartup.Q,
    nDeviceGroup:= 16#4000,
    nIndexOffset:= 16#103,
    nData:= stAxisStruct.config.fDefaultJerk,
    bBusy=>,
    bDone=>,
    bError=>,
    Axis:= stAxisStruct.Axis );]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_Axis">
      <LineId Id="2448" Count="0" />
      <LineId Id="2659" Count="0" />
      <LineId Id="2185" Count="0" />
      <LineId Id="2203" Count="0" />
      <LineId Id="2189" Count="0" />
      <LineId Id="2377" Count="2" />
      <LineId Id="2190" Count="0" />
      <LineId Id="2380" Count="0" />
      <LineId Id="2193" Count="0" />
      <LineId Id="2381" Count="0" />
      <LineId Id="2194" Count="1" />
      <LineId Id="2383" Count="0" />
      <LineId Id="2197" Count="0" />
      <LineId Id="2384" Count="0" />
      <LineId Id="2198" Count="0" />
      <LineId Id="2385" Count="0" />
      <LineId Id="2199" Count="0" />
      <LineId Id="2437" Count="0" />
      <LineId Id="2200" Count="0" />
      <LineId Id="2438" Count="0" />
      <LineId Id="2573" Count="0" />
      <LineId Id="2493" Count="0" />
      <LineId Id="2201" Count="0" />
      <LineId Id="2446" Count="0" />
      <LineId Id="1627" Count="1" />
      <LineId Id="2515" Count="0" />
      <LineId Id="2595" Count="0" />
      <LineId Id="2591" Count="0" />
      <LineId Id="2586" Count="2" />
      <LineId Id="2585" Count="0" />
      <LineId Id="2594" Count="0" />
      <LineId Id="2516" Count="0" />
      <LineId Id="1629" Count="0" />
      <LineId Id="1632" Count="3" />
      <LineId Id="1637" Count="0" />
      <LineId Id="1642" Count="0" />
      <LineId Id="1921" Count="0" />
      <LineId Id="1925" Count="1" />
      <LineId Id="2837" Count="0" />
      <LineId Id="2835" Count="1" />
      <LineId Id="2205" Count="0" />
      <LineId Id="2719" Count="0" />
      <LineId Id="2207" Count="0" />
      <LineId Id="2209" Count="0" />
      <LineId Id="2838" Count="0" />
      <LineId Id="2841" Count="1" />
      <LineId Id="2762" Count="0" />
      <LineId Id="3054" Count="0" />
      <LineId Id="1657" Count="0" />
      <LineId Id="2366" Count="0" />
      <LineId Id="2220" Count="0" />
      <LineId Id="2222" Count="3" />
      <LineId Id="2238" Count="0" />
      <LineId Id="2236" Count="0" />
      <LineId Id="2239" Count="0" />
      <LineId Id="2241" Count="1" />
      <LineId Id="2442" Count="0" />
      <LineId Id="2244" Count="0" />
      <LineId Id="2254" Count="0" />
      <LineId Id="2219" Count="0" />
      <LineId Id="2258" Count="0" />
      <LineId Id="2443" Count="0" />
      <LineId Id="2260" Count="0" />
      <LineId Id="2262" Count="2" />
      <LineId Id="2274" Count="0" />
      <LineId Id="2273" Count="0" />
      <LineId Id="2276" Count="3" />
      <LineId Id="2281" Count="0" />
      <LineId Id="2290" Count="1" />
      <LineId Id="2294" Count="4" />
      <LineId Id="2303" Count="0" />
      <LineId Id="2310" Count="0" />
      <LineId Id="2343" Count="0" />
      <LineId Id="2504" Count="0" />
      <LineId Id="2575" Count="0" />
      <LineId Id="2497" Count="0" />
      <LineId Id="2506" Count="0" />
      <LineId Id="2570" Count="1" />
      <LineId Id="2574" Count="0" />
      <LineId Id="2572" Count="0" />
      <LineId Id="2569" Count="0" />
      <LineId Id="2577" Count="0" />
      <LineId Id="2583" Count="0" />
      <LineId Id="2576" Count="0" />
      <LineId Id="2361" Count="1" />
      <LineId Id="2498" Count="0" />
      <LineId Id="2582" Count="0" />
      <LineId Id="2511" Count="0" />
      <LineId Id="2584" Count="0" />
      <LineId Id="2513" Count="0" />
      <LineId Id="2444" Count="0" />
      <LineId Id="2364" Count="0" />
      <LineId Id="2344" Count="0" />
      <LineId Id="2311" Count="0" />
      <LineId Id="2329" Count="1" />
      <LineId Id="2888" Count="0" />
      <LineId Id="2331" Count="5" />
      <LineId Id="2934" Count="0" />
      <LineId Id="2992" Count="3" />
      <LineId Id="2339" Count="0" />
      <LineId Id="2309" Count="0" />
      <LineId Id="2216" Count="0" />
      <LineId Id="1733" Count="0" />
      <LineId Id="1936" Count="1" />
      <LineId Id="2770" Count="0" />
      <LineId Id="2769" Count="0" />
      <LineId Id="2772" Count="0" />
      <LineId Id="2777" Count="2" />
      <LineId Id="2771" Count="0" />
      <LineId Id="1941" Count="1" />
      <LineId Id="2651" Count="2" />
      <LineId Id="1945" Count="30" />
      <LineId Id="1979" Count="3" />
      <LineId Id="2501" Count="0" />
      <LineId Id="1992" Count="0" />
      <LineId Id="2502" Count="0" />
      <LineId Id="1993" Count="1" />
      <LineId Id="2980" Count="1" />
      <LineId Id="2989" Count="2" />
      <LineId Id="2987" Count="1" />
      <LineId Id="2982" Count="0" />
      <LineId Id="2984" Count="2" />
      <LineId Id="2983" Count="0" />
      <LineId Id="1995" Count="6" />
      <LineId Id="2272" Count="0" />
      <LineId Id="1934" Count="0" />
      <LineId Id="202" Count="1" />
    </LineIds>
    <LineIds Name="FB_Axis.A_SetParameters">
      <LineId Id="52" Count="1" />
      <LineId Id="111" Count="0" />
      <LineId Id="55" Count="4" />
      <LineId Id="114" Count="2" />
      <LineId Id="60" Count="0" />
      <LineId Id="188" Count="8" />
      <LineId Id="61" Count="0" />
      <LineId Id="197" Count="0" />
      <LineId Id="62" Count="4" />
      <LineId Id="118" Count="1" />
      <LineId Id="117" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="75" Count="5" />
      <LineId Id="124" Count="1" />
      <LineId Id="123" Count="0" />
      <LineId Id="81" Count="6" />
      <LineId Id="127" Count="1" />
      <LineId Id="126" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="150" Count="7" />
      <LineId Id="149" Count="0" />
      <LineId Id="89" Count="5" />
      <LineId Id="130" Count="1" />
      <LineId Id="129" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="96" Count="5" />
      <LineId Id="133" Count="1" />
      <LineId Id="132" Count="0" />
      <LineId Id="1" Count="0" />
      <LineId Id="138" Count="0" />
      <LineId Id="140" Count="7" />
      <LineId Id="139" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="160" Count="7" />
      <LineId Id="159" Count="0" />
      <LineId Id="168" Count="0" />
      <LineId Id="170" Count="7" />
      <LineId Id="169" Count="0" />
      <LineId Id="178" Count="0" />
      <LineId Id="180" Count="7" />
      <LineId Id="179" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>