<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_PneumaticAxis" Id="{0ba43c5d-0b1b-4802-81a2-a56c90343a83}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PneumaticAxis

VAR
    _stPneumaticAxis: REFERENCE TO ST_PneumaticAxisStruct;
    fbTimerExtend: TON; //Timing the extraction of the cylinder
    fbTimerRetract: TON; //Timing the retraction of the cylinder
    fbTimerAirPressureErrorLow: TON; //Timer to check the fluctuation of the low air pressure value
    fbTimerAirPressureErrorHigh: TON; //Timer to check the fluctuation of the high air pressure value
    fbTimerAirPressureNoErrorLow: TON; //Timer to check the fluctuation of the low air pressure value
    fbTimerAirPressureNoErrorHigh: TON; //Timer to check the fluctuation of the high air pressure value
    fbTrigRisingEdgePermit: R_TRIG; //Rising edge detection for Permit signal
    fbTrigExtend: R_TRIG; //Rising edge detection to Extend cylinder
    fbTrigRetract: R_TRIG; //Rising edge detection to Retract cylinder
    fbTrigExtendManual: R_TRIG; //Rising edge detection for manual push button
    fbTrigRetractManual: R_TRIG; //Rising edge detection for manual push button
    bStateChange: BOOL := FALSE; //State of the cylinder changed from extend to retract and vice versa
    bStartExtendPLC: BOOL; //Variable to extend cylinder from PLC
    bStartRetractPLC: BOOL; //Variable to retract cylinder from PLC
    bStartExtendManual: BOOL; //Variable to extend cylinder with momentary push button
    bStartRetractManual: BOOL; //Variable to retract cylinder with momentary push button
    bInterlockedExtended: BOOL := FALSE;
    bInterlockedRetracted: BOOL := FALSE;
    bNoPSSPermitError: BOOL := FALSE;
    bStartInitDelay: BOOL := FALSE;
    tInitializeTimer: TON;
    bInitialized: BOOL := FALSE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[tInitializeTimer(IN := bStartInitDelay, PT := T#1S);

IF tInitializeTimer.Q THEN
    bStartInitDelay := FALSE;
    bInitialized := TRUE;
END_IF

IF bInitialized THEN
    mRun();

        CASE _stPneumaticAxis.ePneumaticAxisMode OF

        E_PneumaticMode.eSingleSolenoidControl:
        mSingleSolenoid();

        E_PneumaticMode.eNoPSSPermit:
        mNoPSSPermit();

        E_PneumaticMode.eNoPSSPermitError:
        mNoPSSPermitError();

        E_PneumaticMode.eCylinderRetractInterlocked:
        mRetractedInterlocked();

        E_PneumaticMode.eCylinderExtendInterlocked:
        mExtendedInterlocked();

        E_PneumaticMode.eCylinderRetractInterlockedError:
        mRetractedInterlockedError();

        E_PneumaticMode.eCylinderExtendInterlockedError:
        mExtendedInterlockedError();

        E_PneumaticMode.eLowAirPressure:
        mLowAirPressure();

        E_PneumaticMode.eHighAirPressure:
        mHighAirPressure();

        E_PneumaticMode.eError:
        mSingleSolenoidError();

        E_PneumaticMode.eReset:
        mReset();

        END_CASE
END_IF




















]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{5454029e-f817-018f-32fe-52a7c013d503}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bStartInitDelay := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckEndSwitchBwdStatus" Id="{fde97b3f-ebc0-087e-124e-79f9c467bc30}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckEndSwitchBwdStatus

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn AND NOT bStateChange AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoSignalFromEndSwitchBwd;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckEndSwitchFwdStatus" Id="{2421d0d3-9422-0ef2-10bf-967b1b7d731a}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckEndSwitchFwdStatus

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn AND bStateChange AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended THEN
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoSignalFromEndSwitchFwd;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckForAirPressureError" Id="{02975cbe-1049-025d-3e6b-ceb11cb4a2a0}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckForAirPressureError : BOOL
VAR
    bAirPressureoutOfRangeError : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbTimerAirPressureErrorLow(IN :=,PT:=INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nAllowTimePressureOutOfRange)*1000, Q=>,ET=>);
fbTimerAirPressureErrorHigh(IN :=,PT:=INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nAllowTimePressureOutOfRange)*1000, Q=>,ET=>);
fbTimerAirPressureNoErrorLow(IN :=,PT:=INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nAllowTimePressureOutOfRange)*1000, Q=>,ET=>);
fbTimerAirPressureNoErrorHigh(IN :=,PT:=INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nAllowTimePressureOutOfRange)*1000, Q=>,ET=>);

//Check if the air pressure value is too low for longer than defined time
IF _stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled < _stPneumaticAxis.stPneumaticAxisConfig.fLowLimitPressureValue  THEN
    fbTimerAirPressureErrorLow.IN := TRUE;
    IF fbTimerAirPressureErrorLow.Q THEN
        _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
        bAirPressureoutOfRangeError := TRUE;
        _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eAirPressureErrorLow;
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eLowAirPressure;
    END_IF


//Check if the air pressure value is too high for longer than defined time
ELSIF _stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled > _stPneumaticAxis.stPneumaticAxisConfig.fHighLimitPressureValue  THEN
    fbTimerAirPressureErrorHigh.IN := TRUE;
    IF fbTimerAirPressureErrorHigh.Q THEN
        _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
        bAirPressureoutOfRangeError := TRUE;
        _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eAirPressureErrorHigh;
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eHighAirPressure;
    END_IF


//If the air pressure value is in range, no errors
ELSIF (_stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled >= _stPneumaticAxis.stPneumaticAxisConfig.fLowLimitPressureValue
     AND _stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled <= _stPneumaticAxis.stPneumaticAxisConfig.fHighLimitPressureValue) THEN
    fbTimerAirPressureErrorLow.IN := FALSE;
    fbTimerAirPressureErrorHigh.IN := FALSE;
    bAirPressureoutOfRangeError := FALSE;
END_IF




]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckForMovingError" Id="{c3d6d062-4852-0e9f-18de-b8a77172336a}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckForMovingError

]]></Declaration>
      <Implementation>
        <ST><![CDATA[//IF the command to extract is given, but the cylinder hasn't moved from the bEndSwitchBwd
IF fbTimerExtend.Q AND _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNotMovingExtract;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF

//If the command to extract has timed out
IF fbTimerExtend.Q
   AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted
   AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eExtractTimedOut;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF

//IF the command to retract has been given, but the cylinder hasn't moved from bEndSwitchFwd
IF fbTimerRetract.Q AND _stPneumaticAxis.stPneumaticAxisStatus.bExtended THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNotMovingRetract;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF

//If the command to retract has timed out
IF fbTimerRetract.Q
   AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended
   AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eRetractTimedOut;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eError;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckStatusOfInterlockSignal" Id="{cc7fcaed-155e-0884-115d-338b746a2dd3}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckStatusOfInterlockSignal

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _stPneumaticAxis.stPneumaticAxisConfig.bPSSPneumaticAxisShutter THEN
    _stPneumaticAxis.stPneumaticAxisControl.bInterlock := FALSE;
    ELSIF _stPneumaticAxis.stPneumaticAxisControl.bInterlock THEN
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'WARNING: INTERLOCK ON, CAN NOT MOVE';
        IF _stPneumaticAxis.stPneumaticAxisStatus.bRetracted OR _stPneumaticAxis.stPneumaticAxisStatus.bRetracting THEN
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eCylinderRetractInterlocked;
        END_IF
        IF _stPneumaticAxis.stPneumaticAxisStatus.bExtended OR _stPneumaticAxis.stPneumaticAxisStatus.bExtending THEN
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eCylinderExtendInterlocked;
        END_IF
END_IF






]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCheckStatusOfPermitSignal" Id="{a6169d52-c0e0-0319-04a8-3ea0650a4765}">
      <Declaration><![CDATA[METHOD PUBLIC mCheckStatusOfPermitSignal

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _stPneumaticAxis.stPneumaticAxisConfig.bPSSPneumaticAxisShutter THEN
    _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit := FALSE;
ELSIF NOT _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit THEN
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eNoPSSPermit;
    ELSIF _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit THEN
        _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
        _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;

END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="mExtendedInterlocked" Id="{51cc7600-2ccc-454d-ab69-1386ac754d66}">
      <Declaration><![CDATA[METHOD mExtendedInterlocked


]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := TRUE;
_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
IF NOT _stPneumaticAxis.stPneumaticAxisControl.bInterlock THEN
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
END_IF
IF (_stPneumaticAxis.stPneumaticAxisControl.bRetract OR _stPneumaticAxis.stPneumaticAxisInputs.bCloseManual) OR bInterlockedExtended THEN
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eExtendInterlocked;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eCylinderExtendInterlockedError;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mExtendedInterlockedError" Id="{c0b4e652-3937-42fe-a869-ee6da9ba93ff}">
      <Declaration><![CDATA[METHOD mExtendedInterlockedError

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := TRUE;
bInterlockedExtended := TRUE;
_stPneumaticAxis.stPneumaticAxisInputs.bCloseManual := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: AXIS INTERLOCKED';
_stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;
IF NOT _stPneumaticAxis.stPneumaticAxisControl.bInterlock THEN
    bInterlockedExtended := FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="mHighAirPressure" Id="{a41851e4-8444-48e2-8f12-5249a689e7bc}">
      <Declaration><![CDATA[METHOD mHighAirPressure : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: AIR PRESSURE TOO HIGH';
_stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;
_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
bStateChange := FALSE;
bStartExtendPLC := FALSE;
bStartExtendManual := FALSE;
bStartRetractManual := FALSE;
bStartRetractPLC := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;

IF _stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled <= _stPneumaticAxis.stPneumaticAxisConfig.fHighLimitPressureValue THEN
    fbTimerAirPressureNoErrorHigh.IN := TRUE;
    IF fbTimerAirPressureNoErrorHigh.Q THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
    fbTimerAirPressureErrorHigh.IN := FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
    END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mLowAirPressure" Id="{c0310bb5-42a6-4cfb-b918-7f4740f67c8e}">
      <Declaration><![CDATA[METHOD mLowAirPressure : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: AIR PRESSURE TOO LOW';
_stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;
_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
bStateChange := FALSE;
bStartExtendPLC := FALSE;
bStartExtendManual := FALSE;
bStartRetractManual := FALSE;
bStartRetractPLC := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;

IF _stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled >= _stPneumaticAxis.stPneumaticAxisConfig.fLowLimitPressureValue THEN
    fbTimerAirPressureNoErrorLow.IN := TRUE;
    IF fbTimerAirPressureNoErrorLow.Q THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
    fbTimerAirPressureErrorLow.IN := FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
    END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mNoPSSPermit" Id="{992f1dea-d5e2-43e5-8693-024ad7503be1}">
      <Declaration><![CDATA[METHOD mNoPSSPermit

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisControl.bInterlock := FALSE;
_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'WARNING: NO PERMIT SIGNAL.';
IF (_stPneumaticAxis.stPneumaticAxisControl.bExtend OR _stPneumaticAxis.stPneumaticAxisInputs.bOpenManual OR _stPneumaticAxis.stPneumaticAxisControl.bRetract OR _stPneumaticAxis.stPneumaticAxisInputs.bCloseManual) OR bNoPSSPermitError THEN
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoPSSPermit;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eNoPSSPermitError;
END_IF
IF _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit THEN
        _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
        _stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
        _stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
        bStateChange := FALSE;
        bStartExtendPLC := FALSE;
        bStartExtendManual := FALSE;
        bStartRetractManual := FALSE;
        bStartRetractPLC := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
        _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;

END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mNoPSSPermitError" Id="{4bd71f8e-be7a-40c7-9cce-4b92b8835477}">
      <Declaration><![CDATA[METHOD mNoPSSPermitError

]]></Declaration>
      <Implementation>
        <ST><![CDATA[bNoPSSPermitError := TRUE;
_stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;
_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
bStateChange := FALSE;
bStartExtendPLC := FALSE;
bStartExtendManual := FALSE;
bStartRetractManual := FALSE;
bStartRetractPLC := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: NO PERMIT SIGNAL.';

IF _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit THEN
        bNoPSSPermitError := FALSE;
        _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
        _stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
        _stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
        bStateChange := FALSE;
        bStartExtendPLC := FALSE;
        bStartExtendManual := FALSE;
        bStartRetractManual := FALSE;
        bStartRetractPLC := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
        _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
        _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
        _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;

END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mReset" Id="{4721b129-abfa-4113-85bb-a13eb59dedea}">
      <Declaration><![CDATA[METHOD PUBLIC mReset

]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Reset all the error messages
//Set the operating mode to Single Solenoid Control
fbTrigRisingEdgePermit(clk:=_stPneumaticAxis.stPneumaticAxisControl.bReset);
IF _stPneumaticAxis.stPneumaticAxisControl.bReset THEN
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
    _stPneumaticAxis.stPneumaticAxisControl.bReset := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mRetractedInterlocked" Id="{119414ab-e2e9-44e0-b866-f00dc58d7c9f}">
      <Declaration><![CDATA[METHOD mRetractedInterlocked

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
IF NOT _stPneumaticAxis.stPneumaticAxisControl.bInterlock THEN
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
END_IF
IF (_stPneumaticAxis.stPneumaticAxisControl.bExtend OR _stPneumaticAxis.stPneumaticAxisInputs.bOpenManual) OR bInterlockedRetracted THEN
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eRetractInterlocked;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eCylinderRetractInterlockedError;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mRetractedInterlockedError" Id="{f0f9b367-fd49-420b-9b3d-ab1f518c85fd}">
      <Declaration><![CDATA[METHOD mRetractedInterlockedError

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
bInterlockedRetracted := TRUE;
_stPneumaticAxis.stPneumaticAxisInputs.bOpenManual := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: AXIS INTERLOCKED';
_stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;
IF NOT _stPneumaticAxis.stPneumaticAxisControl.bInterlock THEN
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;
    bInterlockedRetracted:= FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'NO_ERRORS_READY_TO_START';
    _stPneumaticAxis.ePneumaticAxisErrors := E_PneumaticAxisErrors.eNoError;
    _stPneumaticAxis.ePneumaticAxisMode := E_PneumaticMode.eSingleSolenoidControl;
END_IF



]]></ST>
      </Implementation>
    </Method>
    <Method Name="mRun" Id="{4ff666c5-8aa4-4eaa-803d-5700cbef55c2}">
      <Declaration><![CDATA[METHOD PUBLIC mRun
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Set the current status of the end position switches, and permit and intelock signals
mSetInputStatuses();

//Enter allowed times for extraction and retraction of pneumatic cylinder in seconds
mSetAllowedTravelTime();

//Get the value of the air pressure in mb
_stPneumaticAxis.stPneumaticAxisInputs.fPressureValueScaled := mScaledPressureValue(nRawValue :=  _stPneumaticAxis.stPneumaticAxisInputs.nAirPressureValueRaw, fRawHigh := 30518, fRawLow :=0, fScaledHigh := 10, fScaledLow := 0 );

//Check if air pressure is in range
mCheckForAirPressureError();

//Reset Error
mReset();

]]></ST>
      </Implementation>
    </Method>
    <Method Name="mScaledPressureValue" Id="{3012bd32-5e56-08e5-357d-6a59fe398239}">
      <Declaration><![CDATA[METHOD PUBLIC mScaledPressureValue : REAL
VAR_INPUT
    nRawValue: INT; //Raw value from the Terminal
    fRawHigh: REAL; //Highest value for the Raw value
    fRawLow: REAL; //Lowest value for the Raw value
    fScaledHigh: REAL; //Highest value for the Scaled value
    fScaledLow: REAL; //Lowest value for the Scaled value
END_VAR
VAR
    rGradient: REAL; //Internal variable to calculate gradient of the slope
    rOffset: REAL; //Internal variable to calculate the offset value
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[rGradient:= (fScaledHigh - fScaledLow)/(fRawHigh - fRawLow);
rOffset:= fScaledHigh - rGradient * fRawHigh;
mScaledPressureValue:= rGradient * nRawValue + rOffset;


]]></ST>
      </Implementation>
    </Method>
    <Method Name="mSetAllowedTravelTime" Id="{ee25c044-23c7-095b-0652-d0275aeaa21a}">
      <Declaration><![CDATA[METHOD PUBLIC mSetAllowedTravelTime

]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Enter allowed times for extraction and retraction of pneumatic cylinder in seconds
fbTimerExtend.PT := INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nTimeToExtend)*1000;
fbTimerRetract.PT := INT_TO_TIME( _stPneumaticAxis.stPneumaticAxisConfig.nTimeToRetract)*1000;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mSetInputStatuses" Id="{85a914cc-13e6-05a5-383f-5d3b917db237}">
      <Declaration><![CDATA[METHOD PUBLIC mSetInputStatuses
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Set the inputs of limit switches to bExtracted and bRetracted states of the pneumatic cylinders
//Check if cylinder is moving: extending or retracting
_stPneumaticAxis.stPneumaticAxisStatus.bRetracted := _stPneumaticAxis.stPneumaticAxisInputs.bEndSwitchBwd;
_stPneumaticAxis.stPneumaticAxisStatus.bExtended := _stPneumaticAxis.stPneumaticAxisInputs.bEndSwitchFwd;
_stPneumaticAxis.stPneumaticAxisStatus.bSolenoidActive := _stPneumaticAxis.stPneumaticAxisInputs.bSolenoidActive;
_stPneumaticAxis.stPneumaticAxisStatus.bPSSPermitOK := _stPneumaticAxis.stPneumaticAxisInputs.bPSSPermit;
_stPneumaticAxis.stPneumaticAxisStatus.bInterlocked := _stPneumaticAxis.stPneumaticAxisControl.bInterlock;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mSingleSolenoid" Id="{7947fd9c-1dec-078f-0fa8-640a9c506249}">
      <Declaration><![CDATA[METHOD PUBLIC mSingleSolenoid

]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Check if Ithe PSS signal for shutter is ON
mCheckStatusOfPermitSignal();

//Check if Interlock signal is ON
mCheckStatusOfInterlockSignal();

//Starting the timing of extraction movement
fbTimerExtend(IN := _stPneumaticAxis.stPneumaticAxisStatus.bExtending);
_stPneumaticAxis.stPneumaticAxisStatus.nTimeElapsedExtend := TIME_TO_INT(fbTimerExtend.ET);

fbTrigExtend(clk:=_stPneumaticAxis.stPneumaticAxisControl.bExtend);
IF fbTrigExtend.Q THEN
   bStartExtendPLC := TRUE;
END_IF

//If the manual momentary push button is used for control of the pneumatic cylinder
fbTrigExtendManual(clk:=_stPneumaticAxis.stPneumaticAxisInputs.bOpenManual);
IF fbTrigExtendManual.Q THEN
   bStartExtendManual := TRUE;
END_IF

//Starting the extraction movement
IF (bStartExtendPLC OR bStartExtendManual) THEN
    _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := TRUE;
    IF NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended
       AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
        _stPneumaticAxis.stPneumaticAxisStatus.bExtending := TRUE;
    END_IF
    //If the command to retract is given while the cylinder is extracting
    IF NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended
          AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
        IF (bStartRetractPLC OR bStartRetractManual) THEN
            _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
            _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := TRUE;
            _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
            _stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
            bStartExtendPLC := FALSE;
            bStartExtendManual := FALSE;
        END_IF
    END_IF
END_IF

// Extraction of the cylinder finished, reset timer
IF (bStartExtendPLC OR bStartExtendManual) AND _stPneumaticAxis.stPneumaticAxisStatus.bExtended THEN
    bStateChange := TRUE;
    _stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
    _stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
    bStartExtendPLC := FALSE;
    bStartExtendManual := FALSE;
END_IF

//Check if LimitSwitchBwd signal is ON
mCheckEndSwitchFwdStatus();


//Starting the retraction movement and retraction timing
fbTimerRetract (IN := _stPneumaticAxis.stPneumaticAxisStatus.bRetracting);
_stPneumaticAxis.stPneumaticAxisStatus.nTimeElapsedRetract := TIME_TO_INT(fbTimerRetract.ET);

fbTrigRetract(clk:=_stPneumaticAxis.stPneumaticAxisControl.bRetract);
IF fbTrigRetract.Q THEN
   bStartRetractPLC := TRUE;
END_IF

//If the manual momentary push button is used for control of the pneumatic cylinder
fbTrigRetractManual(clk:=_stPneumaticAxis.stPneumaticAxisInputs.bCloseManual);
IF fbTrigRetractManual.Q THEN
   bStartRetractManual := TRUE;
END_IF

IF (bStartRetractPLC OR bStartRetractManual) THEN
    _stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
    IF NOT _stPneumaticAxis.stPneumaticAxisStatus.bExtended
       AND NOT _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
        _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := TRUE;
    END_IF
END_IF

// Retraction of the cylinder finished, reset timer
IF  (bStartRetractPLC OR bStartRetractManual) AND _stPneumaticAxis.stPneumaticAxisStatus.bRetracted THEN
    bStateChange := FALSE;
    _stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;
    _stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
    bStartRetractPLC := FALSE;
    bStartRetractManual := FALSE;
END_IF
//Check if LimitSwitchFwd signal is ON
mCheckEndSwitchBwdStatus();


mCheckForMovingError();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mSingleSolenoidError" Id="{60e07ee6-93c1-0a35-3946-686c9080e0f9}">
      <Declaration><![CDATA[METHOD PUBLIC mSingleSolenoidError

]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE  _stPneumaticAxis.ePneumaticAxisErrors OF

    E_PneumaticAxisErrors.eNoError:
    _stPneumaticAxis.stPneumaticAxisStatus.bError := FALSE;

    E_PneumaticAxisErrors.eExtractTimedOut:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: COMMAND TIMED OUT';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

    E_PneumaticAxisErrors.eRetractTimedOut:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: COMMAND TIMED OUT';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

    E_PneumaticAxisErrors.eNotMovingExtract:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: CYLINDER NOT MOVING';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

    E_PneumaticAxisErrors.eNotMovingRetract:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: CYLINDER NOT MOVING';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

    E_PneumaticAxisErrors.eNoSignalFromEndSwitchBwd:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: NO SIGNAL FROM END SWITCH BWD';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

    E_PneumaticAxisErrors.eNoSignalFromEndSwitchFwd:
    _stPneumaticAxis.stPneumaticAxisStatus.sStatus := 'ERROR: NO SIGNAL FROM LIMIT SWITCH FWD';
    _stPneumaticAxis.stPneumaticAxisStatus.bError := TRUE;

END_CASE

_stPneumaticAxis.stPneumaticAxisControl.bExtend := FALSE;
_stPneumaticAxis.stPneumaticAxisControl.bRetract := FALSE;
_stPneumaticAxis.stPneumaticAxisOutputs.bValveOn := FALSE;
bStateChange := FALSE;
bStartExtendPLC := FALSE;
bStartExtendManual := FALSE;
bStartRetractManual := FALSE;
bStartRetractPLC := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bExtending := FALSE;
_stPneumaticAxis.stPneumaticAxisStatus.bRetracting := FALSE;


]]></ST>
      </Implementation>
    </Method>
    <Property Name="stPneumaticAxis" Id="{32b385f3-2648-4c7a-b2e7-2316433a21ee}">
      <Declaration><![CDATA[PROPERTY PUBLIC stPneumaticAxis : REFERENCE TO ST_PneumaticAxisStruct
]]></Declaration>
      <Get Name="Get" Id="{adb8011b-822d-0d9a-11a8-4f5c0c464fc5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[stPneumaticAxis REF= _stPneumaticAxis;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{83f042d6-81e0-4e48-8d59-f688cd15c5d1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_stPneumaticAxis REF= stPneumaticAxis;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>